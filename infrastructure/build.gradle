import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.21'

    id 'org.openapi.generator' version '7.2.0'
//	id 'org.jlleitschuh.gradle.ktlint' version '11.0.0'
}

java {
    sourceCompatibility = '17'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.20'
    implementation 'io.swagger.core.v3:swagger-models:2.2.20'

//    compileOnly 'jakarta.annotation:jakarta.annotation-api:2.1.1'

//    implementation 'org.springdoc:springdoc-openapi-data-rest:1.6.13'
//    implementation 'org.springdoc:springdoc-openapi-ui:1.6.13'
//    implementation 'org.springdoc:springdoc-openapi-kotlin:1.6.13'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo.spring30x:4.7.0'

    implementation project(':domain:products')
}

tasks.withType(KotlinCompile) {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

sourceSets {
    main {
        kotlin.srcDirs += "$buildDir/generated/open-api/server/src/main/kotlin"
    }
}

openApiGenerate {
    generatorName = 'kotlin-spring'
    library = 'spring-boot'
    inputSpec = "$rootDir/infrastructure/src/main/resources/open-api/server/product-oas.yaml" as String
    outputDir = "$buildDir/generated/open-api/server" as String
    packageName = 'com.eightbits.eco.retail.infrastructure.generated.v1'
    apiPackage = 'com.eightbits.eco.retail.infrastructure.generated.v1.api'
    modelPackage = 'com.eightbits.eco.retail.infrastructure.generated.v1.model'
    invokerPackage = 'com.eightbits.eco.retail.infrastructure.generated.v1.invoker'
    enablePostProcessFile = true
    skipOverwrite = false
    configOptions = [
            interfaceOnly       : 'true',
            useSpringBoot3      : 'true',
            enumPropertyNaming  : "original",
            serializationLibrary: 'jackson',
            dateLibrary         : 'java8'
    ]
    importMappings = [
            Locale: 'java.util.Locale',
    ]
}

compileKotlin.dependsOn tasks.openApiGenerate
compileTestKotlin.dependsOn tasks.openApiGenerate

//ktlint {
//	filter {
//		exclude("**/generated/**")
//	}
//}
